plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "net.ltgt.apt-eclipse" version "0.18"
    id "net.ltgt.apt-idea" version "0.18"
    id 'com.adarshr.test-logger' version '1.6.0'
}

apply plugin:"application"
apply plugin:"java"
apply plugin: "jacoco"

version "0.1"
group "com.github.alvarosanchez.micronaut"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.0.1'
    }
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-security-jwt"
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut.configuration:micronaut-jdbc-hikari"
    compile "io.micronaut:micronaut-inject"
    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-runtime"
    compile "io.micronaut.configuration:micronaut-hibernate-jpa"
    compile "io.micronaut:micronaut-http-server-netty"
    compile "org.springframework.security:spring-security-crypto:4.2.5.RELEASE"
    compileOnly "io.micronaut:micronaut-inject-java"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "com.h2database:h2"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testCompile "io.micronaut.test:micronaut-test-junit5:1.0.0.RC2"
    testCompile "io.micronaut:micronaut-inject-java"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "com.github.alvarosanchez.micronaut.todo.Application"
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}
// use JUnit 5 platform
test {
    useJUnitPlatform()
}

testlogger {
    theme 'mocha' // project level
    slowThreshold 3_000
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.79
            }
        }
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/github/alvarosanchez/micronaut/todo/**/$*',
                    'com/github/alvarosanchez/micronaut/todo/Application.class'
            ])
        })
    }
}

check.dependsOn jacocoTestCoverageVerification
build.finalizedBy jacocoTestReport